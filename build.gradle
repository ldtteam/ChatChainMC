buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "io.freefair.gradle:lombok-plugin:6.2.0"
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "io.freefair.lombok"
apply plugin: "com.github.johnrengelman.shadow"

group = modGroup
archivesBaseName = modBaseName
version = System.getenv()['BUILD_NUMBER'] != null ? System.getenv()['BUILD_NUMBER'] : System.getenv()['TRAVIS_BUILD_NUMBER']

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

processResources {
    duplicatesStrategy = 'include'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

repositories {
    mavenLocal()
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    mavenCentral()
    jcenter()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.75'

    compileOnly('org.jetbrains:annotations:15.0')

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    //compile('ninja.leaping.configurate:configurate-core:3.3')

    compileOnly 'co.chatchain:commons:42978'
    shadow 'co.chatchain:commons:42978'
}

minecraft {
    mappings channel: 'official', version: '1.17.1'

    runs {
        client {
            workingDirectory project.file('runClient')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                structurize {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('runServer')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            args 'nogui'

            mods {
                structurize {
                    source sourceSets.main
                }
            }
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]

    exclude 'dummyThing'

    //relocate('com.typesafe.config', 'co.chatchain.typesafe.config')
    relocate('com.google', 'co.chatchain.google')
    relocate('javax.inject', 'co.chatchain.inject')
}

reobf {
    shadowJar {}
}

task setupDecompWorkspace {
    afterEvaluate {
        println "Setup"
    }
}

task setupCIWorkspace {
    afterEvaluate {
        println "Setup"
    }
}
